plugins {
	id 'java-library'
	id 'eclipse'
}

ext {
	javacppVersion = '1.5.5'
	cudaVersion = '11.2-8.1'
}

version "$cudaVersion-$javacppVersion"

repositories {
    maven { url 'https://repo.gradle.org/gradle/libs-releases' }
    mavenCentral()
}

sourceSets {
	main {
		resources {
			srcDir "$buildDir/native"
		}
	}
}

dependencies {
	implementation platform("org.springframework.boot:spring-boot-dependencies:2.5.0")

	implementation "org.slf4j:slf4j-simple:1.7.30"
	implementation "org.bytedeco:javacpp-platform:$javacppVersion"
	implementation "org.bytedeco:cuda-platform:$cudaVersion-$javacppVersion"

	testImplementation 'org.junit.jupiter:junit-jupiter'
	testImplementation 'org.assertj:assertj-core'
	testImplementation 'org.mockito:mockito-core'
}

task javacppCompile(type: JavaExec) {
	// Executable jars can have only _one_ jar on the classpath.
	classpath = files(configurations.runtimeClasspath.asPath)
	mainClass = 'org.bytedeco.javacpp.tools.Builder'
	workingDir = sourceSets.main.java.outputDir
	
	// port 5005; can debug using eclipse Remote Java Application
	debug = false
	
	def arch = "windows-x86_64"
	
	doFirst {
		// arguments to pass to the application
		// args "thrust/Thrust"
		
		args "org/bytedeco/cuda/presets/cub"
		args "-properties", "${arch}-cuda"
		args "-nodelete"
		args "-Xcompiler", "--gpu-architecture=sm_61"
		// issue with /GL and Thurst: https://github.com/bytedeco/javacpp/issues/485
		args "-Xcompiler", "-Xcompiler=-GL-"
		args "-d", "${buildDir}/native"
	}
}

compileJava.finalizedBy javacppCompile

test {
	useJUnitPlatform()
	testLogging {
		events "passed", "skipped", "failed"
	}
}
