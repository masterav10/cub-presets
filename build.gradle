plugins {
	id 'java-library'
	id 'eclipse'
	id 'antlr'
	id 'org.inferred.processors' version '3.3.0'
}

ext {
	javacppVersion = '1.5.5'
	cudaVersion = '11.2-8.1'
	arch = 'windows-x86_64'
}

version "$cudaVersion-$javacppVersion"

repositories {
    maven { url 'https://repo.gradle.org/gradle/libs-releases' }
    mavenCentral()
}

sourceSets {
	javacpp {
		compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
		java {
			srcDirs = ["${buildDir}/javacpp/java"]
		}
		resources {
			srcDirs = ["${buildDir}/javacpp/resources"]
		}
	}
}

configurations {
    javacppImplementation.extendsFrom implementation
    javacppRuntimeOnly.extendsFrom runtimeOnly
}

dependencies {
	antlr 'org.antlr:antlr4:4.7.2'

	processor 'org.inferred:freebuilder:2.7.0'
	implementation platform("org.springframework.boot:spring-boot-dependencies:2.5.0")

	implementation "org.slf4j:slf4j-simple"
	implementation "org.bytedeco:javacpp-platform:$javacppVersion"
	implementation "org.bytedeco:cuda-platform:$cudaVersion-$javacppVersion"

	testImplementation 'org.junit.jupiter:junit-jupiter'
	testImplementation 'org.assertj:assertj-core'
	testImplementation 'org.mockito:mockito-core'
}

generateTestGrammarSource {
	outputDirectory = file("build/generated-src/antlr/main/org/bytedeco/grammars")
    arguments += ["-package", "org.bytedeco.grammars"]
}

task javacppGenerate(type: JavaExec, dependsOn: [compileJava]) {
	def outputDir = "${buildDir}/javacpp/java"
	
	inputs.dir(sourceSets.main.java.outputDir)
	outputs.dirs(outputDir)

	// Executable jars can have only _one_ jar on the classpath.
	classpath = files(configurations.runtimeClasspath.asPath)
	mainClass = 'org.bytedeco.javacpp.tools.Builder'
	workingDir = sourceSets.main.java.outputDir
	
	// port 5005; can debug using eclipse Remote Java Application
	debug = false
	
	doFirst {
		// arguments to pass to the application
		args "org/bytedeco/cuda/presets/cub"
		args "-properties", "${arch}-cuda"
		args "-d", "${outputDir}"
	}
}

compileJavacppJava.dependsOn javacppGenerate

task jniJavacpp(type: JavaExec, dependsOn: compileJavacppJava) {
	// Executable jars can have only _one_ jar on the classpath.
	classpath = files(sourceSets.javacpp.compileClasspath)
	mainClass = 'org.bytedeco.javacpp.tools.Builder'
	workingDir = sourceSets.javacpp.java.outputDir
	
	// port 5005; can debug using eclipse Remote Java Application
	debug = false
	
	doFirst {
		// arguments to pass to the application
		// args "thrust/Thrust"		
		
		def input = sourceSets.javacpp.java.classesDirectory.get()
		fileTree(input).each {
			args it.toString()
				   .replace(input.toString(), "")
				   .replace("\\", "/")
				   .replace(".class", "")
				   .substring(1)
		}
		
		args "-properties", "${arch}-cuda"
		args "-nodelete"
		args "-Xcompiler", "--gpu-architecture=sm_61"
		// issue with /GL and Thurst: https://github.com/bytedeco/javacpp/issues/485
		args "-Xcompiler", "-Xcompiler=-GL-"
		args "-d", "${buildDir}/javacpp/resources/${arch}"
	}
}

task generateAll(dependsOn: jniJavacpp)

jar.dependsOn generateAll

jar {
	from sourceSets.javacpp.java
}

test {
	useJUnitPlatform()
	testLogging {
		events "passed", "skipped", "failed"
	}
}
